// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // i ? ? a cccccc ddd jjj

    Not(in=instruction[15], out=Ains);
    And(a=instruction[15], b=true, out=Cins);

    Mux16(a=instruction, b=ALUout, sel=Cins, out=ARegin);
    Or(a=Ains, b=instruction[5], out=ARegLoad);
    ARegister(in=ARegin, load=ARegLoad, out=A, out[0..14]=addressM);

    And(a=Cins, b=instruction[4], out=DRegLoad);
    DRegister(in=ALUout, load=DRegLoad, out=D);

    And(a=Cins, b=instruction[3], out=writeM);

    And(a=Cins, b=instruction[12], out=AtoM);
    Mux16(a=A, b=inM, sel=AtoM, out=AM);

    ALU(x=D, y=AM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], 
    f=instruction[7], no=instruction[6], out=ALUout, out=outM, zr=zrOut, ng=ngOut);

    Not(in=zrOut, out=notzr);
    Not(in=ngOut, out=pos);
    And(a=notzr, b=pos, out=posnotzero);

    And(a=Cins, b=instruction[0], out=jgt);
    And(a=jgt, b=posnotzero, out=jmp1);

    And(a=Cins, b=instruction[1], out=jeq);
    And(a=jeq, b=zrOut, out=jmp2);
        
    And(a=Cins, b=instruction[2], out=jlt);
    And(a=jlt, b=ngOut, out=jmp3);

    Or(a=jmp1, b=jmp2, out=jmp);
    Or(a=jmp, b=jmp3, out=PCload);

    PC(in=A, load=PCload, inc=true, reset=reset, out[0..14]=pc);
}